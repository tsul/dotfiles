# Modified from 'minimal' prompt

function +vi-git_status {
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red} ✗%f'
  fi
}

function prompt_water_precmd {
  vcs_info
}

function prompt_water_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_water_precmd

  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{green} ✓%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red} ✗%f'
  zstyle ':vcs_info:*' formats '%b%c%u'
  zstyle ':vcs_info:*' actionformats "%b%c%u|%F{cyan}%a%f"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  PROMPT='%B%F{black}%2~ %F{blue}› %b%f'
  RPROMPT='%B%F{black} on %F{blue}${vcs_info_msg_0_}%b%f'
}

prompt_water_setup "$@"
